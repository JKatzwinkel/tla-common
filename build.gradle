plugins {
    id 'jacoco'
    id 'checkstyle'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.44.0'
    id 'com.github.dawnwords.jacoco.badge' version '0.2.4'
}

group = 'com.github.jkatzwinkel'
version = '0.0.909'
sourceCompatibility = '17'

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'tla-common'
            pom {
                name = 'TLA Commons'
                description = 'DTO model and utility functions for the Thesaurus Linguae Aegyptiae web publication components'
            }

            from components.java
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://projectlombok.org/edge-releases' } // until lombok 1.18.26 release in maven cental
}

dependencies {
    compileOnly 'org.projectlombok:lombok:edge-SNAPSHOT'
    annotationProcessor 'org.projectlombok:lombok:edge-SNAPSHOT'

    api 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
    implementation 'org.slf4j:slf4j-api:2.0.6'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
    finalizedBy 'generateJacocoBadge'
}

check {
    dependsOn 'jacocoTestCoverageVerification'
}

checkstyle {
    toolVersion '8.29'
    configFile file("config/checkstyle/checkstyle.xml")
    ignoreFailures = true
    maxErrors = 100
}
checkstyleMain {
    configProperties += [
        'checkstyle.header.file': file('config/checkstyle/java.header'),
        'checkstyle.regexp.header.file': file('config/checkstyle/java_regexp.header')
    ]
}
checkstyleTest {
    configFile file("config/checkstyle/checkstyle-tests.xml")
    configProperties += [
        'checkstyle.header.file': file('config/checkstyle/java.header'),
        'checkstyle.regexp.header.file': file('config/checkstyle/java_regexp.header')
    ]
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.1
           }
        }
    }
}
